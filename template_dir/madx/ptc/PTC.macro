PTCmodel=2;
PTCmethod=6;
PTCnst=1;

PTCchroma : macro={
ptc_create_universe;
  ptc_create_layout,model=PTCmodel,method=PTCmethod,nst=PTCnst, exact=true;
  PTC_SETSWITCH, debuglevel=0, fringe=true,exact_mis=true;
  select_ptc_normal, q1=0, q2=0; //fractional tune
  select_ptc_normal, dq1=1, dq2=1; // chromaticity
  select_ptc_normal, dq1=2, dq2=2; // second order chrom
  select_ptc_normal, dq1=3, dq2=3; // third order chrom 
  select_ptc_normal, anhx=1,0,0, anhy=1,0,0;    // 1st order
  select_ptc_normal, anhx=0,1,0, anhy=0,1,0;

  PTC_ALIGN; 
 
  ptc_normal,deltap=dp,closed_orbit,normal,icase=5,no=4;
  qx0 =table(normal_results,value,1);
  qx1 =table(normal_results,value,3);
  qx2 =table(normal_results,value,5);
  qx3 =table(normal_results,value,7); 
  qy0 =table(normal_results,value,2);
  qy1 =table(normal_results,value,4);
  qy2 =table(normal_results,value,6);
  qy3 =table(normal_results,value,8); 
  axx =table(normal_results,value,9); 
  axy =table(normal_results,value,10); 
  ayy =table(normal_results,value,12);
 
  value,  qx0, qy0, qx1, qy1, qx2, qy2, qx3, qy3, axx, axy, ayy;
  write, table=normal_results, file="PTC-normal.ptc";
ptc_end;
};


PTCtunes(dp): macro={
ptc_create_universe;
  ptc_create_layout,model=PTCmodel,method=PTCmethod,nst=PTCnst, exact=true;
  PTC_SETSWITCH, debuglevel=0, fringe=true, exact_mis=true;
  select_ptc_normal, q1=0, q2=0; //fractional tune
  PTC_ALIGN;
  ptc_normal,deltap=dp,closed_orbit,normal,icase=5,no=4;
  option, -info;
  qx0 =table(normal_results,value,1);
  qy0 =table(normal_results,value,2);
  option, info;
  value, qx0, qy0;
ptc_end;
};

